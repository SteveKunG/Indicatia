import groovy.json.JsonOutput
import groovy.json.JsonSlurper

architectury {
    common(project.enabled_platforms.split(","))
}

dependencies {
    modImplementation "net.fabricmc:fabric-loader:${fabric_loader_version}"

    modImplementation("me.shedaniel.cloth:cloth-config-fabric:${cloth_config_version}") {
        exclude group: "net.fabricmc.fabric-api"
        exclude group: "net.fabricmc", module: "fabric-loader"
    }
}

processResources {
    doLast {
        minifyJson(outputs)
    }
}

loom {
    mixin {
        defaultRefmapName = "mixins.indicatia.refmap.json"
    }
}

def minifyJson(TaskOutputs outputs) {
    fileTree(dir: outputs.files.asPath, include: ["**/*.json", "**/*.mcmeta"]).each {
        def file -> file.text = JsonOutput.toJson(new JsonSlurper().parse(file))
    }
}