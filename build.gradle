plugins {
    id "architectury-plugin" version "3.4-SNAPSHOT"
    id "com.github.johnrengelman.shadow" version "7.1.2" apply false
    id "dev.architectury.loom" version "0.12.0-SNAPSHOT" apply false
    id "com.matthewprenger.cursegradle" version "1.4.0" apply false
    id "com.modrinth.minotaur" version "2.+" apply false
}

architectury {
    minecraft = project.mc_version
}

subprojects {
    apply plugin: "dev.architectury.loom"
    apply plugin: "com.github.johnrengelman.shadow"
    apply plugin: "com.matthewprenger.cursegradle"
    apply plugin: "com.modrinth.minotaur"

    loom {
        silentMojangMappingsLicense()
    }

    repositories {
        maven {
            name "ParchmentMC"
            url "https://maven.parchmentmc.org"
        }
    }

    dependencies {
        minecraft "com.mojang:minecraft:${mc_version}"
        mappings loom.layered() {
            officialMojangMappings()
            parchment("org.parchmentmc.data:parchment-${parchmentmc_version}@zip")
        }
    }
}

allprojects {
    apply plugin: "java"
    apply plugin: "architectury-plugin"

    archivesBaseName = project.archives_base_name
    version = project.mod_version
    group = project.maven_group

    repositories {
        maven {
            name "CurseMaven"
            url "https://www.cursemaven.com/"
            content {
                includeGroup "curse.maven"
            }
        }
        maven {
            name "DevAuth"
            url "https://pkgs.dev.azure.com/djtheredstoner/DevAuth/_packaging/public/maven/v1"
        }
    }

    tasks.withType(JavaCompile) {
        options.encoding = "UTF-8"
        options.release = 17
    }

    java {
        withSourcesJar()
    }
}

task curseforgeModrinth(dependsOn: [
        ":fabric:modrinth",
        ":fabric:curseforge",
        ":forge:modrinth",
        ":forge:curseforge"
], group: "upload")

if (project.hasProperty("version_lib_dir")) {
    task updateVersionLib(group: "upload") {
        def versionFile = file("${version_lib_dir}/indicatia_version.json")
        def jsonBuilder = new groovy.json.JsonBuilder(new groovy.json.JsonSlurper().parseText(versionFile.text))
        def jsonElement = jsonBuilder.content
        def formattedList = []

        jsonElement.promos."${mc_version}-latest" = project.mod_version
        jsonElement.promos."${mc_version}-recommended" = project.mod_version

        def reader = new BufferedReader(new FileReader(rootProject.file("CHANGE_LOG.md")));
        def line;
        for (int ln = 0; (line = reader.readLine()) != null; ln++) {
            if (ln >= 4) {
                formattedList.add(line)
            }
        }

        jsonElement."${mc_version}"."${mod_version}" = formattedList.join("\n")
        versionFile.text = jsonBuilder.toPrettyString()
    }
}