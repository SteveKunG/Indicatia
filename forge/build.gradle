architectury {
    platformSetupLoomIde()
    forge()
}

loom {
    clientOnlyMinecraftJar()

    forge {
        mixinConfigs = ["mixins.indicatia.json", "mixins.indicatia_forge.json"]
    }

    runs {
        client {
            vmArg("-Dmixin.debug.export=true")
            vmArg("-Dmixin.hotSwap=true")
            vmArg("-Ddevauth.configDir=${devauth_dir}")
        }
    }

    mixin {
        defaultRefmapName = "mixins.indicatia_forge.refmap.json"
    }
}

configurations {
    common
    shadowCommon
    compileClasspath.extendsFrom common
    runtimeClasspath.extendsFrom common
    developmentForge.extendsFrom common
}

dependencies {
    forge "net.minecraftforge:forge:${project.mc_version}-${project.forge_version}"

    modRuntimeOnly "curse.maven:configured-457570:${project.configured_version}"
    modRuntimeOnly "curse.maven:catalogue-459701:${project.catalogue_version}"
    modRuntimeOnly "me.djtheredstoner:DevAuth-forge-latest:${project.devauth_version}"
    modImplementation "me.shedaniel.cloth:cloth-config-forge:${project.cloth_config_version}"

    common(project(path: ":common", configuration: "namedElements")) { transitive false }
    shadowCommon(project(path: ":common", configuration: "transformProductionForge")) { transitive false }
}

processResources {
    inputs.property "version", project.version

    filesMatching("META-INF/mods.toml") {
        expand "version": project.version
    }
}

String fileName = "${project.archives_base_name}-mc${project.mc_version}"

shadowJar {
    configurations = [project.configurations.shadowCommon]
    archiveClassifier = "dev-shadow"
    archiveVersion = "v${project.version}"
    archiveBaseName = fileName
}

remapJar {
    inputFile.set shadowJar.archiveFile
    dependsOn shadowJar
    archiveClassifier = "forge"
    archiveVersion = "v${project.version}"
    archiveBaseName = fileName
}

components.java {
    withVariantsFromConfiguration(project.configurations.shadowRuntimeElements) {
        skip()
    }
}

curseforge {
    if (project.hasProperty("upload_api_key")) {
        apiKey = project.upload_api_key
        project {
            id = "272146"
            changelog = file("../CHANGE_LOG.md")
            changelogType = "markdown"
            releaseType = "release"
            addGameVersion "Forge"
            addGameVersion "Java 17"
            addGameVersion project.mc_version

            mainArtifact(remapJar.archiveFile) {
                displayName = "[Forge] " + fileName + "-v${project.version}"
            }
            relations {
                requiredDependency "cloth-config"
                optionalDependency "configured"
            }
            afterEvaluate {
                uploadTask.dependsOn(remapJar)
            }
        }
        options {
            forgeGradleIntegration = false
        }
    }
}

import com.modrinth.minotaur.dependencies.ModDependency
import com.modrinth.minotaur.dependencies.DependencyType

modrinth {
    if (project.hasProperty("modrinth_api_token")) {
        token = project.modrinth_api_token
        projectId = "Grr1HrUE"
        changelog = file("../CHANGE_LOG.md").text
        versionName = "[Forge] Indicatia " + "v${project.version}" + " for Minecraft " + project.mc_version
        versionNumber = project.mod_version + "-forge"
        versionType = "release"
        uploadFile = remapJar
        gameVersions = ["${project.mc_version}"]
        loaders = ["forge"]
        dependencies = [
                new ModDependency("9s6osm5g", DependencyType.REQUIRED) // Cloth Config
        ]
    }
}